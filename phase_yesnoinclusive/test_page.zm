/******************************************************************************\
FILE:           test_page.zm
AUTHOR:         Theo Veenker <theo.veenker@beexy.nl>
ADAPTED BY:     Jan de Mooij <A.J.deMooij@uu.nl>

DESCRIPTION:

Provides a page object to show to the participant during test trials.
It handles presenting the stimulus and recording the participant's response.


HISTORY:
2012-01-30 TV   Created.
2016-03-03 AJM  Adapted

\******************************************************************************/

import std_sound_output_device;


Page test_page
{
    TestItem    item;           // trial control parameters

    init()
    {
        fill_pattern_color = TEST_PAGE_COLOR;
    }


    on_event:key_press()
    {
        // Handle left-shift (no) and right-shift (yes).
        if (input_key == KEY_Shift_L)
            response.process_hit(0, event_time);
        else if (input_key == KEY_Shift_R)
            response.process_hit(1, event_time);
        else
            handle_special_key(this, input_key, input_modifiers);
    }

    on_event:message()
    {
        if(message_arg == layout.again.button_response)
        {
            layout.buttons.feedback_provider.start_feedback(layout.again);
            layout.again.repeat_stimulus();
        }
        else
            response.process_hit(message_arg, event_time);
    }

    VerticalLayout layout
    {
        init()
        {
            spacing = 50;
            height = 0;     // 0 means as large as possible
        }


        CanvasGadget canvas
        {
            init()
            {
//              fill_pattern_color = TEST_PAGE_STIMULUS_COLOR;
                size = 450, 100;
                offset_x = width / 2;
                offset_y = height / 2;
            }


            CrossShape fixation
            {
                init()
                {
                    radius = 15;
                    line_width = 5;
                    line_pattern_color = color:crimson;
                }
            }
        }

        HorizontalLayout buttons
        {
            init()
            {
                spacing = 50;
                options = LAYOUT_EQUAL_WIDTH;
            }

            Timer feedback_provider
            {
                ButtonGadget target_button;

                time start_feedback(ButtonGadget button)
                {
                    target_button = button;
                    target_button.fill_pattern_color = color:lightslategray;
                    start(now() + BUTTON_FEEDBACK_DURATION);
                    return this.expected_expire_time;
                }

                on_event:expire()
                {
                    target_button.fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                }
            }

            ButtonGadget button1
            {
                int button_response = 0;

                init()
                {
                    text = NO_BUTTON_TEXT;
                    font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                    font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                    fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                    text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
                    size = 150, 75;
                }

                on_event:activate()
                {
                    response.process_hit(button_response, event_time);
                }
            }


            ButtonGadget button2
            {
                int button_response = 1;

                init()
                {
                    text = YES_BUTTON_TEXT;
                    font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                    font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                    fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                    text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
                    size = 150, 75;
                }


                on_event:activate()
                {
                    response.process_hit(button_response, event_time);
                }
            }
        }


        ButtonGadget again
        {
            int button_response = 30;

            init()
            {
                text = REPEAT_BUTTON_TEXT;
                font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
            }


            on_event:activate()
            {
                repeat_stimulus();
            }

            void repeat_stimulus()
            {
                stimulus.play(event_time + 100ms);
            }
        }


        time start_fixation(time t)
        {
            // Start fixation cross.
            canvas.fixation.start(t, FIXATION_DURATION);

            // Return fixation offset time.
            return canvas.fixation.expected_finish_time;
        }


        void reset()
        {
            full_abort();
            buttons.button1.fill_pattern_color=TEST_PAGE_BUTTON_COLOR;
            buttons.button2.fill_pattern_color=TEST_PAGE_BUTTON_COLOR;
            again.fill_pattern_color=TEST_PAGE_BUTTON_COLOR;
        }
    }


    SoundChain stimulus
    {
        // Sound source/producer object.
        SoundSequence seq {
            SoundFile clip1 {};
            SoundFile clip2 {};
        }


        // Sound sink/consumer object.
        SoundPlayback playback {}


        time play(time t)
        {
            abort();

            playback.device = sound_output_device;
            seq.clip1.file = stimuli_dir() + "sounds/" + item.sndfn1;
            seq.clip2.file = stimuli_dir() + "sounds/" + item.sndfn2;
            seq.clip1.trailer_duration = INTERSTIMULUS_INTERVAL;

            start(t);

            return expected_finish_time;
        }
    }


    Response response
    {
        // Response data.
        int     value;          // response value: 0=no, 1=yes
        int     rt;
        int     correct;
        int     stimoffset;

        void clear()
        {
            value = -1;
            rt = -9999;
        }


        void process_hit(int val, time t)
        {
            HitType validity = hit(t);

            // Accept the first hit in the response interval and end the 
            // response interval.
            if (validity == HIT_VALID)
            {
                value = val;
                correct = val == item.expected_answer;
                rt = int(reaction_time(0));

                time stop_time = now();

                if(layout.buttons.button1.button_response == val)
                    stop_time = layout.buttons.feedback_provider.start_feedback(layout.buttons.button1);
                else
                    stop_time = layout.buttons.feedback_provider.start_feedback(layout.buttons.button2);

                ignore_remaining_hits();

                stop(stop_time);
            }
            else if (validity == HIT_TOO_EARLY || validity == HIT_TOO_LATE)
            {
                // False alarm. Deal with this if desired.
            }
        }

        on_event:finish()
        {
            done(CONTINUE);
        }
    }

    // Aborts any ongoing activity on this page and signals the initiator
    // of the trial that we're done.
    void done(int msgid)
    {
        // Just in case; abort presentation if still active.
        layout.reset();
        stimulus.abort();

        signal_target(msgid);   // tell caller we're done
        target = null;

        control.clear_status();
    }


    //==========================================================================


    // Performs preparatory work required before using this page.
    void setup(int ntrials=-1, bool pr=false)
    {
        test_page_overlay.setup(this);

        // Show or hide prompt.
        test_page_overlay.show_prompt(pr ? 
            YESNOINCLUSIVE_QUESTION : "", true);

        // Enable or disable the progress bar.
        test_page_overlay.show_progress_bar(ntrials);   // disabled if < 0

        control.target = this;
        control.button_box.disable_buttons();
        control.button_box.enable_button(0, layout.buttons.button1.button_response); // button 0 means no(0)
        control.button_box.enable_button(2, layout.buttons.button2.button_response); // button 2 means yes(1)
        control.button_box.enable_button(1, layout.again.button_response);
    }


    // Performs cleaning up if necessary.
    void cleanup()
    {
        test_page_overlay.cleanup();
    }


    // Starts a trial.
    void action(Object caller, TestItem it, time tref, int count=-1)
    {
        // Save the passed trial control parameters.
        item = it;


        // Show this page (if not yet done) and make tref the trial start time.
        tref = test_window1.show_test_page(this, tref + INTERTRIAL_INTERVAL);


        // Reset response data.
        response.clear();


        // Start fixation symbol.
        time fixoffset = layout.start_fixation(tref);


        // Start stimulus at fixation offset.
        time stimoffset = stimulus.play(fixoffset);
        response.stimoffset = int(stimoffset - fixoffset);

        // Response interval starts at stimulus offset.
        response.start(fixoffset);

        // Update progress counter (if enabled).
        test_page_overlay.update_progress_bar(count);

        // Update status line on control window. Cleared in done() above.
        control.set_status(
            string(item.id) + "  " + 
            string(item.type) + "  " + 
            string(item.sndfn1)  + "  " + 
            string(item.sndfn2) + " " + string(item.expected_answer));

        // Remember who to signal when the trial is over.
        target = caller;
    }
}
